// ========================================
// MOD BUILDING TASKS
// ========================================

tasks.named('javadoc', Javadoc) {
    options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

tasks.named('jar').configure {
    archiveBaseName.set(mod_name.replace(" ", ""))
    archiveVersion.set("${minecraft_version}-(v.${mod_version}-${mod_loader})")
}

tasks.matching { it.name == 'runData' }.configureEach {
    finalizedBy('spotlessApply')
}

tasks.matching { it.name == 'runClient' }.configureEach {
    dependsOn('spotlessApply')
}


// ========================================
// BUILDING AND FORMATTING
// ========================================

//fix for more than 65535 entries
tasks.withType(Zip).configureEach {
    zip64 = true
}


java {
    withSourcesJar()
    withJavadocJar()
}


spotless {
    java {
        target 'src/**/*.java'
        eclipse().configFile("${rootDir}/eclipse-java-formatter.xml")
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    json {
        target 'src/**/*.json'
        gson().indentWithSpaces(2)
        endWithNewline()
    }
    groovyGradle {
        target '**/*.gradle'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}
